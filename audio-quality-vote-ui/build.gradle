plugins {
	id "java"
	id "war"
	id "org.gretty"
	id "eclipse-wtp" 
	id "org.wisepersist.gwt"
	id "com.diffplug.eclipse.apt"
}

compileJava.options.encoding = 'UTF-8'
compileJava.options.release = 11

compileTestJava.options.encoding = 'UTF-8'
compileTestJava.options.release = 11

java.toolchain.languageVersion = JavaLanguageVersion.of(11)
java.toolchain.vendor = JvmVendorSpec.ADOPTOPENJDK

tasks.withType(org.wisepersist.gradle.plugins.gwt.GwtCompile) {
	args "-optimize", "0", "-noincremental"
}

tasks.withType(org.wisepersist.gradle.plugins.gwt.GwtSuperDev) {
	args "-style", "PRETTY", "-strict"
	jvmArgs "-Dgwt.watchFileChanges=false"
}

gwt {
	logLevel = 'INFO'
 
	maxHeapSize = "2048M";
 
	gwtVersion='2.9.0'
 
	modules 'com.cherokeelessons.audio.quality.AudioQualityVote'
	
	compiler {
		src += files("../audio-quality-vote-shared/src/main/resources", "../audio-quality-vote-shared/src/main/java")
		src += files(compileJava.options.annotationProcessorGeneratedSourcesDirectory)
		
		disableClassMetadata = false;
		
		strict = true;
		style = "PRETTY";
		localWorkers = 2;
	}
	
	superDev {
		src += files("../audio-quality-vote-shared/src/main/resources", "../audio-quality-vote-shared/src/main/java")
		src += files(project.eclipse.jdt.apt.genSrcDir)
		
		noPrecompile = true;
		failOnError = false;
		bindAddress = "0.0.0.0";
	}
	
	eclipse {
		addGwtContainer = true;
	}
}

war {
	rootSpec.exclude('**/*.class')
}

dependencies {
	implementation project(':audio-quality-vote-shared')

	compileOnly 'com.google.gwt:gwt-servlet:'+gwt.gwtVersion
	compileOnly 'com.google.gwt:gwt-user:'+gwt.gwtVersion
	
	//compileOnly "io.github.freddyboucher:gwt-oauth2:1.1"
	compileOnly "com.github.CherokeeLanguage.gwt-oauth2:gwt-oauth2:1.2-RC1"
	
	compileOnly ('javax.inject:javax.inject:1')
	compileOnly ('com.google.dagger:dagger-compiler:2.36')
	compileOnly ('com.google.dagger:dagger-gwt:2.36')
	annotationProcessor ('com.google.dagger:dagger-compiler:2.36')
	
	compileOnly 'com.ekotrope:gwt-completablefuture:1.0.0'
	
	compileOnly 'org.fusesource.restygwt:restygwt:2.2.7'
	compileOnly "com.fasterxml.jackson.core:jackson-annotations:2.11.3"
	compileOnly "com.fasterxml.jackson.core:jackson-databind:2.11.3"
	compileOnly 'com.google.code.findbugs:jsr305:3.0.0' //required for restygwt compile
	compileOnly 'com.github.nmorel.gwtjackson:gwt-jackson:0.15.4'
	compileOnly 'javax.xml.bind:jaxb-api:2.3.+'
 
	compileOnly project(':audio-quality-vote-shared')
	compileOnly 'javax.xml.bind:jaxb-api:2.3.+'
	
	//ui
	compileOnly 'com.github.gwtmaterialdesign:gwt-material:2.4.+'
	compileOnly 'com.github.gwtmaterialdesign:gwt-material-addins:2.4.+'
	compileOnly 'com.github.gwtmaterialdesign:gwt-material-table:2.4.+'
	compileOnly 'com.github.gwtmaterialdesign:gwt-material-themes:2.4.+'
	compileOnly 'com.google.elemental2:elemental2-dom:1.1.+'
	
	//restapi
	compileOnly 'javax.servlet:javax.servlet-api:3.0.1'
	compileOnly 'javax.ws.rs:javax.ws.rs-api:2.1.1'
}

// Required for correct setup of APT in Eclipse
eclipseJdt.dependsOn cleanEclipseJdt, eclipseJdtApt
eclipseJdtApt.dependsOn cleanEclipseJdtApt, eclipseFactorypath
eclipseFactorypath.dependsOn cleanEclipseFactorypath

eclipse {
	    // Required for correct setup of APT in Eclipse
	    synchronizationTasks eclipseJdt

	project {
		name = 'Audio Quality Vote (UI)'
	}
	classpath {
		downloadSources=true
	}
}